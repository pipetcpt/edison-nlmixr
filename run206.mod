;; 1. Based on:
;; 2. Description: Final PK model
;; x1. Author: user
;; 3. Label:
$PROBLEM    Final PK model
$INPUT      ID TIMEM=DROP TIME RAWDV=DROP DV AMT IIMIN=DROP II ADDL
            CMT DOSE=DROP OXY=DROP SEX PERIOD=DROP WATER=DROP MDV
            AGE=DROP HT=DROP WT SCR=DROP AST=DROP ALT=DROP
; Units
;; DV = g/L
;; AMT = g
$DATA      ..\data\PKdata_ver5.csv IGNORE=#
$SUBROUTINE ADVAN6 TOL=6
$MODEL      NCOMP=2 COMP=(DEPOT) COMP=(CENTRAL)
$PK
;;; VMSEX-DEFINITION START
IF(SEX.EQ.1) VMSEX = 1  ; Most common
IF(SEX.EQ.2) VMSEX = ( 1 + THETA(8))
;;; VMSEX-DEFINITION END

;;; VM-RELATION START
VMCOV=VMSEX
;;; VM-RELATION END

;;; VCWT-DEFINITION START
VCWT = ( 1 + THETA(7)*(WT - 69))
;;; VCWT-DEFINITION END

;;; VC-RELATION START
VCCOV=VCWT
;;; VC-RELATION END

; Population parameter
TVKA = THETA(1)
TVVC = THETA(2)
TVVC = VCCOV*TVVC
TVVM = THETA(3)
TVVM = VMCOV*TVVM
TVKM = THETA(4)

; Individual parameter
KA = TVKA; * EXP(ETA(1))
VC = TVVC * EXP(ETA(1))
VM = TVVM * EXP(ETA(2))
KM = TVKM; * EXP(ETA(2))

S2 = VC
;KEL = CL/VC

$DES
CONC = A(2)/VC
DADT(1) =  -KA*A(1)
DADT(2) = KA*A(1) - VM*CONC/(KM+CONC)

$ERROR
IPRED = F
    W = SQRT(THETA(5)**2*IPRED**2 + THETA(6)**2)
    Y = IPRED + W*EPS(1)
 IRES = DV-IPRED
IWRES = IRES/W

$THETA  (0,8.09658) ; 1.KA
 (0,73.6782) ; 2.VC
 (0,9.64917) ; 3.Vmax
 (0,0.0408837) ; 4. Km
 (0,0.179277) ; Prop.RE (sd)
 (0,0.001) FIX ; Add.RE (sd)
$THETA  (-0.111,0.016739,0.045) ; VCWT1
$THETA  (-1,-0.235138,5) ; VMSEX1
$OMEGA  0.0498046  ;         VC
 0.0116147  ;         VM
;0.47 2.71  ; KM
$SIGMA  1  FIX  ; Proportional error PK
$ESTIMATION METHOD=1 INTER MAXEVAL=5000 NOABORT NSIG=3 SIGL=6 PRINT=5
; Xpose
$TABLE ID TIME DV MDV IPRED IWRES CWRES ONEHEADER NOPRINT FILE=sdtab206
$TABLE ID VC KA VM KM ETA(1) ETA(2) ONEHEADER NOPRINT FILE=patab206
;$TABLE ID SEX ONEHEADER NOPRINT FILE=catab206
;$TABLE ID AGE HT WT SCR AST ALT ONEHEADER NOPRINT FILE=cotab206
$COVARIANCE

